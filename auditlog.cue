// SPDX-License-Identifier: Apache-2.0

// Schema lifecycle: experimental | stable | deprecated
@gemara(status="experimental")
package gemara

import "list"

@go(gemara)

// AuditLog records results from an audit performed against a target resource
#AuditLog: {
	#Log
	metadata: type: "AuditLog"

	// owner defines the RACI roles responsible for managing the audit
	owner?: #RACI @go(Owner)

	// summary provides the high-level conclusion
	summary: string

	// criteria defines the acceptable state for the audited resource
	criteria: [#ArtifactMapping, ...#ArtifactMapping]

	// results records audit results against the criteria
	results: [#AuditResult, ...#AuditResult] @go(Results,type=[]*AuditResult)

	if results != _|_ {
		_uniqueResultIds: {for i, r in results {(r.id): i}}
		let _validCriteriaIds = [for c in criteria {c."reference-id"}]

		// Unify the valid ID list with a list.Contains constraint to require each result scores against declared criteria
		for i, r in results {
			_criteriaValidation: "\(i)": _validCriteriaIds & list.Contains(r."criteria-reference"."reference-id")
		}
	}
}

// ResultType classifies the nature of an audit result
#ResultType: "Gap" | "Finding" | "Observation" | "Strength" @go(-)

// AuditResult records a single result with supporting evidence and recommendations.
#AuditResult: {
	// id uniquely identifies this result
	id: string

	// title describes this result at a glance
	title: string

	// type classifies the nature of this result
	type: #ResultType

	// description explains the result in detail
	description: string

	// criteria-reference maps this result to specific criteria entries
	"criteria-reference": #MultiEntryMapping @go(CriteriaReference)

	// evidence records the data sources that support this result
	evidence?: [#Evidence, ...#Evidence] @go(Evidence)
	evidence?: [#_EvidenceStrict, ...#_EvidenceStrict]

	// recommendations records corrective actions for this result
	recommendations?: [#Recommendation, ...#Recommendation] @go(Recommendations)
}

// Recommendation provides a corrective action for an audit result
#Recommendation: {
	// id uniquely identifies this recommendation
	id?: string

	// text describes the recommended corrective action
	text: string

	// required indicates whether this recommendation is a mandatory corrective action
	required: *false | bool @gemara(default=false)
}

// Evidence records what was cited to support an opinion for a specific activity:
// raw data for the evaluation layer, evaluation and enforcement artifacts for the audit layer.
// At least one of payload or source MUST be present; an entry with neither is semantically incomplete.
#Evidence: {
	// id uniquely identifies this evidence
	id: string

	// type categorizes the kind of evidence
	type: #EvidenceType

	// collected-at is the timestamp when the evidence was gathered
	"collected-at": #Datetime @go(CollectedAt)

	// payload is the raw evidence data collected inline
	payload?: _ @go(Payload,type=any)

	// source identifies the artifact or system from which this evidence was collected
	source?: #EvidenceMapping @go(Source)

	// description explains what this evidence represents
	description?: string

	// retention records the obligations governing how long this evidence must be kept
	retention?: #Retention @go(Retention,optional=nillable)
}

// _EvidenceStrict layers the "at least one of payload or source" rule on top of #Evidence
#_EvidenceStrict: {
	@go(-)
} & #Evidence & {
	payload?: _
	if payload == _|_ {
		source: #EvidenceMapping
	}
}

// EvidenceType categorizes the kind of evidence. It remains an open enum:
// recommended values include artifact types already known to Gemara (e.g.
// EvaluationLog, EnforcementLog) plus categories for common evidence forms.
#EvidenceType: #ArtifactType | string @go(-)

// Retention records the obligations governing how long referenced evidence must
// be kept, and any preservation holds suspending their disposition.
// Conflicting obligations are recorded side by side rather than reconciled by the producer.
#Retention: {
	// obligations records each retention rule that applies to this evidence and
	// the instant it resolves to
	obligations?: [#RetentionObligationRef, ...#RetentionObligationRef]

	// holds records preservation holds in effect when this artifact was published.
	// While any entry lacks released, the referenced evidence must not be destroyed,
	// regardless of any effective instant in obligations, including a maximum.
	// Absence of this field is not evidence that no hold exists: a hold issued after
	// publication cannot appear in an immutable artifact.
	holds?: [#PreservationHold, ...#PreservationHold]

	if holds != _|_ {
		_uniqueHoldIds: {for i, h in holds {(h.id): i}}
	}
}

// RetentionObligationRef ties a resolved instant to the obligation that produced it.
#RetentionObligationRef: {
	// obligation references a retention-obligations entry in the governing Policy
	obligation: #EntryMapping

	// effective is the instant this obligation resolves to for this evidence
	effective: #Datetime
}

// PreservationHold records a legal, regulatory, or investigatory hold that
// suspends disposition of the referenced evidence.
#PreservationHold: {
	// id uniquely identifies this hold within the citation
	id: string

	// matter references the legal matter, regulatory order, or hold notice
	matter: #EntryMapping

	// issued is when the hold took effect
	issued: #Datetime

	// released is when the hold was lifted; absent means in effect at publication
	released?: #Datetime
}
